apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories { jcenter() }

configurations {
    jvmBoot { description "additional libs on jvm bootclasspath" }
}

ext { jettyVersion = '9.3.6.v20151106' }

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile "org.eclipse.jetty:jetty-server:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-proxy:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
    compile "org.eclipse.jetty.http2:http2-server:${jettyVersion}"
    runtime 'ch.qos.logback:logback-classic:1.1.3'
    testCompile 'junit:junit:4.12'
    jvmBoot('org.mortbay.jetty.alpn:alpn-boot:8.1.6.v20151105')
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
        plusConfigurations += [ configurations.jvmBoot ]
    }
}

idea {
    module {
        scopes.COMPILE.plus += [ configurations.jvmBoot ]
    }
}

mainClassName = "jettyproxy.ProxyServer"
applicationDefaultJvmArgs =["-Xbootclasspath/p:$configurations.jvmBoot.singleFile"]

wrapper {
    gradleVersion = '2.8'
}